define Start(lPar)
{
## (c) 2004 Shane T. Mueller
## stmuelle@indiana.edu
## This is a redesigned version of the cue experiment 'cue.pbl'
## In this experiment, each word set is comprised of a bunch
## of 4-word similar sets (6 low-freq, 6 high-freq) and 12 unrelated
## foils.  From these larger sets, we select either 1, 2, or
## 3 items to use in the encoding phase; we then cue with a non-
## presented related word (and the unrelated foils).  The most interesting
## aspect is the 'fill-in' task, where subjects fill in partially-completed words
## as an incidental task.

  #Create stimuli and cue lists.

  wordlists <- FileReadTable("wordsets-new.txt")

  trialStimuliList <- []
  trialCueList     <- []
  gBlockNum <- 1

  ##Figure out the subject number; it is 0 unless it is
  ## in with the -v command-line parameter.

  if(IsList(lPar))
     {
	    gSubNum   <- ToNumber(First(lPar))
	  } else  {
       gSubNum <- 0
    }


	 
  datafilename <- "data/x0402-" + gSubNum + ".dat"
  gFileOut <- FileOpenWrite(datafilename)

   Print("Running Subject " + gSubNum + ". Saving to file [" + datafilename + "].")

  ##This loop creates a list of lists containing everything
  ##needed for running a single 'trial'; the presentation of a
  ##list and cues.


  loop(wordlist, wordlists)
  {
   ##The following commands extract the data and cues from the wordlists, and labels them according to condition.
	Print(wordlist)
   Print("---------------------------------")
	stim <- []
   cues <- []

	##sample two from each of the first two sets of four
   tmp <- Shuffle(SubList(wordlist,1,4))
	stim <- Append(stim, First(tmp))
   cues <- Append(cues, Nth(tmp,2))	

   tmp <- Shuffle(SubList(wordlist,5,8))
	stim <- Append(stim, First(tmp))
   cues <- Append(cues, Nth(tmp,2))	

	##sample three from each of the second two sets of four
   tmp <- Shuffle(SubList(wordlist,9,12))
	stim <- Merge(stim, SubList(tmp, 1,2))
   cues <- Append(cues, Nth(tmp,3))	

   tmp <- Shuffle(SubList(wordlist,13,16))
	stim <- Merge(stim, SubList(tmp,1,2))
   cues <- Append(cues, Nth(tmp,3))	

	##sample all four from the third two sets of four
   tmp <- Shuffle(SubList(wordlist,17,20))
	stim <- Merge(stim, SubList(tmp, 1,3))
   cues <- Append(cues, Nth(tmp,4))	

   tmp <- Shuffle(SubList(wordlist,21,24))
	stim <- Merge(stim, SubList(tmp,1,3))
   cues <- Append(cues, Nth(tmp,4))	

   ######
	##Now do the same for the next six sets of four. 
	##sample two from each of the first two sets of four
   
	tmp <- Shuffle(SubList(wordlist,25,28))
	stim <- Append(stim, First(tmp))
   cues <- Append(cues, Nth(tmp,2))	

   tmp <- Shuffle(SubList(wordlist,29,32))
	stim <- Append(stim, First(tmp))
   cues <- Append(cues, Nth(tmp,2))	

	##sample three from each of the second two sets of four
   tmp <- Shuffle(SubList(wordlist,33,36))
	stim <- Merge(stim, SubList(tmp, 1,2))
   cues <- Append(cues, Nth(tmp,3))	

   tmp <- Shuffle(SubList(wordlist,37,40))
	stim <- Merge(stim, SubList(tmp,1,2))
   cues <- Append(cues, Nth(tmp,3))	

	##sample all four from the third two sets of four
   tmp <- Shuffle(SubList(wordlist,41,44))
	stim <- Merge(stim, SubList(tmp, 1,3))
   cues <- Append(cues, Nth(tmp,4))	

   tmp <- Shuffle(SubList(wordlist,45,48))
	stim <- Merge(stim, SubList(tmp,1,3))
   cues <- Append(cues, Nth(tmp,4))	

	##Get the rest of the cues.
   cues <- Merge(cues, SubList(wordlist,49,60))

	stimFrequency <- Merge(Repeat("L",12), Repeat("H",12))
	cueFrequency <- Merge(Merge(Repeat("L", 6), Repeat("H",	6)), Repeat("N", 12))
	

	stimCategorySize <- RepeatList([1,1,2,2,2,2,3,3,3,3,3,3],2)
  	cueCategorySize <-  	Merge(RepeatList([1,1,2,2,3,3],2), [0,0,0,0,0,0,0,0,0,0,0,0])

	trialStim <- Transpose([stim, stimFrequency, stimCategorySize])
   trialCues <- Transpose([cues, cueFrequency, cueCategorySize])

	trialStimuliList <- Append(trialStimuliList, trialStim)
	trialCueList <-   Append(trialCueList, trialCues)


  }


  Print("")

  trialList <- Shuffle(Transpose([trialStimuliList, trialCueList, [1,2,3,4]]))
  Print(trialList)

  #Preliminary definitions
  gWin <- MakeWindow("grey")
  fg <- MakeColor("Black")
  bg <- MakeColor("Grey90")
  instructionsFont <- MakeFont("Vera.ttf",0,18,fg, bg,1)
  gStimulusFont <- MakeFont("Vera.ttf", 0, 28, fg, MakeColor("grey"),1)
  gCueFont      <- MakeFont("VeraMono.ttf", 0, 22, MakeColor("red"), MakeColor("grey"),1)
  gInputFont    <- MakeFont("VeraMono.ttf", 0, 22, fg, MakeColor("white"),1)
  
  
  gCorrectFont <-  MakeFont("Vera.ttf",0,20,MakeColor("forestgreen"),MakeColor("white"),1)
  gIncorrectFont <-  MakeFont("Vera.ttf",0,20,MakeColor("red"),MakeColor("white"),1)

   
  ## Get demographic information.
#  GetNIMHDemographics(gSubNum, gWin, "data/x0402-demographics.txt")

  instructionsFillIn <- "For this task, you will see a " +
  "series of 24 word fill-ins. Each fill-in will consist of " +
  "a beginning and end letter, with dashes and letters " +
  "in between. For each fill-in, we want you to guess "+
  "what word this came from.  Once we have determined that "+
  "have learned the words well enough, you will go on to the next stage; "+
  "otherwise, you will repeat the fill-in stage.  This will  " +
  "at least 4 or 5 rounds to finish. "+
  "HINT: if you are unsure of what the word is, leave it "+ 
  "blank and try to remember it for the next round.   When you are ready to begin, hit the X key."

  instructionsPleasantness<-"For this task, you will see a " +
  "series of 24 words. For each word, we want you to think about  "+
  "whether the word describes a pleasant or unpleasant idea. " +
  "If the word is pleasant, press the 'Y' key; if it is "+
  "unpleasant, press the 'N' key.  There are no right or " +
  "wrong answers, but you must choose one of the options "+
  "for each word.  When you are ready to begin, hit the X key."


  instructionsResponse <- "Now, we will show you a series of " +
    "new words.  Some will be related to words from the fill-ins, and "+
    "others won't be.  Read each word, and determine whether it "+ 
    "reminds you of any of the fill-in words from the last fill-in stage.  YOU DO NOT NEED TO " +
    "REMEMBER THE ORIGINAL WORD; simply decide if the current word seems similar "+
    "to one of the previous words.  If you say yes, you will be asked to recall a word " +
    "you were reminded of.  If you are uncertain what word you remember, leave the entry " +
	 "blank or make the best guess you can.  Press the X key to continue."


  instructionsDone <- "Thank you for participating in the experiment!  You may now leave the booth." 





  gInstructions <- MakeTextBox("", instructionsFont, 400, 300)

  gKeyCues <- MakeLabel("",instructionsFont)
  AddObject(gInstructions, gWin)
  Move(gInstructions, 125, 100)


  order <- Mod(gSubNum,2)
  Print("Order: " + order)
  ###Present the stimuli
  loop(trials, trialList)
   {

	##################################################
	#Show the relevant instructions (either base or between-block.)

     gWordSet <- Nth(trials, 3)
     ##Determine which 'incidental' learning task to use.
	  if( Mod(order + gBlockNum,2))
        {
         gEncodingType <- "f"

      	SetText(gInstructions, instructionsFillIn)
			Show(gInstructions)
			Draw()

			WaitForKeyPress("X")
			Hide(gInstructions)


			stimuli <- First(trials)
			cues <- Nth(trials, 2)


        #repeat until they get 12 correct or they do it 4 times.
        times <- 1
        correct <- 0
        gRound <- 1


        #You can stop if you have gone through at least 3 times and
        #you got 18 correct, or if you have gone through 5 times
        while( (times <= 3) or 
		         (times <=5  and times > 3 and correct < 18))
         {
          Print("Round " + times + " Correct: " + correct )
          correct <- PresentListFillIn(stimuli)

          if(times < 3 or (correct < 18 and times >= 3 and times < 5))
			 {
			    SetText(gInstructions,"You need to learn the words a little better.  " +
             "If you are unsure about a word, " +
             "do not spend too much time thinking about it; it is better to leave the response blank " +
				 "and try to remember it for the next round. " +
				 "You will perform another fill-in round, in which you see the same words as before.  Good Luck!" +
             "  Press any key to continue.")
			 } else 
          {
			    SetText(gInstructions,"Good Job. You have " +
              "learned these words well enough to go on to the next stage.  Press any key to continue.")
			 }

          times <- times + 1
          gRound <- gRound + 1  #increment round counter
			 Show(gInstructions)
			 Draw()
          WaitForAnyKeyPress()
			 Hide(gInstructions)
          Draw()
        }
        }else {
 
         gEncodingType <- "p"
    	   SetText(gInstructions, instructionsPleasantness)
			Show(gInstructions)
			Draw()

			WaitForKeyPress("X")
			Hide(gInstructions)


			stimuli <- First(trials)
			cues <- Nth(trials, 2)

         PresentListJudgment(stimuli)
       }

	    #####################################################		
	    #Change the text of the instructions box for use later.
       SetText(gInstructions, instructionsResponse)
 	    Show(gInstructions)
	    Draw(gWin)
    	 WaitForKeyPress("X")
	    Hide(gInstructions)

		
	     PresentCues(cues)
	
       gBlockNum <- gBlockNum + 1
    }
    SetText(gInstructions, instructionsDone)
    Show(gInstructions) 
    Draw(gWin)
    Wait(1000)
    WaitForAnyKeyPress()
    Hide(gInstructions)
    FileClose(gFileOut)
  
}


##This function presents a list as word fill-ins,  keeping track of how many
## are completed correctly.

define PresentListFillIn(stimuli)
{

   #This is where the word is presented
  stimulusLabel <- MakeLabel("---",gStimulusFont)
  AddObject(stimulusLabel, gWin)
  Move(stimulusLabel, 320, 240)
  
   #Keeps track of how many are correct. 
   correct <- 0
  
   #Data
   tb <-MakeTextBox("",gInputFont,200,30)
   AddObject(tb,gWin)
   Move(tb,200,100)
   Show(tb)
  
   AddObject(gKeyCues,gWin)
   Move(gKeyCues, 320, 300)
   Show(gKeyCues)

  
  stimuli <- Shuffle(stimuli)
  spos <- 1
  loop(i, stimuli)
  {
     word <- Nth(i,1)
	  text <- MakeStem(word)
     SetText(stimulusLabel,text)
     SetText(gKeyCues, "[" + spos + "]      " + correct + " of 24."  )
     SetText(tb,"")

	  Show(stimulusLabel)
	  Show(gKeyCues)
	  Draw()
	  time0 <- GetTime()


	  response <- GetInput(tb, "<return>")
	  time1 <- GetTime()

	  ##Add the key cues (defined in the main program)
	  Draw(gWin)
  
          if(Uppercase(response) == Uppercase(word))
          {
             correct <- correct + 1
             SetText(stimulusLabel,"Correct! The word was: " + word)
				 SetFont(stimulusLabel,gCorrectFont)
             trialCorrect <- 1
          } else {
             SetText(stimulusLabel, "Incorrect. The word was: " + word)
				 SetFont(stimulusLabel, gIncorrectFont)
             trialCorrect <- 0
          }

  	 Draw()
    WaitForAnyKeyPress()
    SetFont(stimulusLabel, gStimulusFont)

	  FilePrint_(gFileOut,"PRES: " + gSubNum +
	  " f " + gBlockNum + " " + gWordSet + " "  + gRound + " " + spos + " " + word + " " +Nth(i, 2) + " " +  Nth(i,3) )
	  FilePrint(gFileOut,  " " + (time1 - time0) + " " + trialCorrect + " |" + response ) 
	  spos <- spos + 1
  }
	  RemoveObject(stimulusLabel, gWin)
	  RemoveObject(gKeyCues,gWin)
     RemoveObject(tb,gWin)
     return(correct)
}


##This function presents a list as word fill-ins,  keeping track of how many
## are completed correctly.

define PresentListJudgment(stimuli)
{

   #This is where the word is presented
  stimulusLabel <- MakeLabel("---",gStimulusFont)
  AddObject(stimulusLabel, gWin)
  Move(stimulusLabel, 320, 240)
 
   #Keeps track of how many are correct. 
   correct <- 0
  
   #Data

   AddObject(gKeyCues,gWin)
   Move(gKeyCues, 320, 300)
   Show(gKeyCues)

  
  stimuli <- Shuffle(stimuli)
  spos <- 1
  loop(i, stimuli)
  {
     word <- Nth(i,1)
	  text <- word
     SetText(stimulusLabel,text)
     SetText(gKeyCues, "Trial [" + spos + "] of 24 items. Press 'Y' for pleasant, 'N' for unpleasant."  ) 
	  Show(stimulusLabel)
	  Show(gKeyCues)
	  Draw()
	  time0 <- GetTime()



	  response <-  WaitForListKeyPress(["Y", "N"])
	  time1 <- GetTime()

	  ##Add the key cues (defined in the main program)
	  Draw(gWin)

     SetFont(stimulusLabel, gStimulusFont)

	  FilePrint_(gFileOut,"PRES: " + gSubNum +
	  " p " + gBlockNum + " " + gWordSet + " 1 " + spos + " " + word + " " +Nth(i, 2) + " " +  Nth(i,3) )
	  FilePrint(gFileOut,  " " + (time1 - time0) + " 0 |" + response ) 


	  spos <- spos + 1
  }
	  RemoveObject(stimulusLabel, gWin)
	  RemoveObject(gKeyCues,gWin)
     return(correct)
}



define PresentCues(cues)
{
	 ##This is where the cue is presented
    cueLabel <- MakeLabel("---", gCueFont)
    AddObject(cueLabel, gWin)
    Move(cueLabel,320, 240)

	 ##This is where the participant enters the response.
    tb <-MakeTextBox("",gInputFont,200,30)
    AddObject(tb,gWin)
    Move(tb,200,100)
    Hide(tb)
  
    #This is a little instruction/status bar.
    AddObject(gKeyCues,gWin)
    Move(gKeyCues, 320, 300)
    Show(gKeyCues)


    cues <- Shuffle(cues)
	 spos <- 1
	 ##Loop through each cue list
	 loop(i, cues)
		{

        SetText(gKeyCues,"Hit [Y] if related to earlier word, [N] otherwise.")
             
		  cueText <- First(i)
		  SetText(cueLabel,cueText)
		  Show(cueLabel)
		  time0 <- GetTime()
		  Draw()

		  response <-  WaitForListKeyPress(["Y", "N"])
		  time1 <- GetTime()
		  Hide(cueLabel)
              if(response == "y")
              {
		SetText(gKeyCues, "If you can, enter the word " + First(i) + " reminds you of.")
		Draw()
                Show(tb)
                resp <- GetInput(tb, "<return>")
                SetText(tb,"")
                Hide(tb)
               
                #If they do not enter anything, mark it for easier data processing.
                if(resp=="")
					 {
                  resp <- "<no_response>"
					 }
  
              } else  {
	        resp <- "X"
              }
              
		  FilePrint_(gFileOut,"TEST: " + gSubNum + " "+
		  gEncodingType + " " +  gBlockNum + " " +  gWordSet + " 1 " +  spos + " ")
		  FilePrint_(gFileOut,cueText + " "  + Nth(i, 2) + " " + Nth(i,3))		
	  	  FilePrint_(gFileOut," "  +	  (time1 - time0) + " " + response)
		  FilePrint(gFileOut, " |" + resp ) 
		  spos <- spos + 1
      }   
    Wait(500)

  RemoveObject(gKeyCues,gWin)
  RemoveObject(cueLabel,gWin)
  RemoveObject(tb,gWin)

}



define MakeStem(word)
{
  length <- StringLength(word)

  if(length == 0)
    { 
      stem <- ""
    } else {
    if(length == 1)
	 {
  	  stem <-  "_"
    } else {
      if(length == 2)
        {
          stem <- SubString(word,1,1) + " _"
        } else {
 
           dashes <- ""
	       loop(i, Sequence(2, length-1, 1))
   	     {

             #For long words, replace an intermediate dash with a letter
             if((i == 3) or  (i==6) or (i == 9) or (i == 12))
               { 
                dashes <- dashes + SubString(word,i,1) 
               } else { 	 
                dashes <- dashes + " _ "
               }
           }

	       stem <- SubString(word,1,1) + dashes + SubString(word,length,1)
       }
    }
   } 
   return(stem)
}
